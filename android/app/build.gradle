plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"  
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
}

def dotenv = new Properties()
def envFile = rootProject.file("../.env")
if (envFile.exists()) {
    envFile.withInputStream { stream ->
        dotenv.load(stream)
    }
} else {
    throw new FileNotFoundException("Could not find.env file at: ${envFile.path}")
}

// key.properties 파일 읽기 
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withReader('UTF-8') { reader ->
        keystoreProperties.load(reader)
    }
} else {
    throw new FileNotFoundException("Could not find key.properties file")
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.alom.romrom"
    compileSdk flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        // Enable core library desugaring for libraries that require newer Java APIs
        coreLibraryDesugaringEnabled true
    }

    lint {
        baseline = file("lint-baseline.xml")
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.alom.romrom"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        manifestPlaceholders += [
            NMF_CLIENT_ID      : dotenv['NMF_CLIENT_ID'] ?: "",
            KAKAO_NATIVE_APP_KEY : dotenv['KAKAO_NATIVE_APP_KEY'] ?: ""
        ]
        multiDexEnabled true
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    // Required for core library desugaring (e.g., flutter_local_notifications)
    // Use 2.1.4 or above as required by newer dependencies
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
}
